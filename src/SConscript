#!/bin/env python
##########################################################
#
# scons - buildsystem for aklock
#
##########################################################

aklock_sources = [ 'aklock.c', 'akcursors.c', 'auth_none.c' ]

Import('aklock_env')

build =  aklock_env.Copy()

if build['passwd']:
    aklock_sources += [ 'auth_passwd.c' ]
if build['hash']:
    aklock_sources += [ 'auth_md5.c', 'auth_sha1.c' ]
if build['pam']:
    aklock_sources += [ 'auth_pam.c' ]


aklock = build.Program('aklock', aklock_sources)
build.AddPostAction(aklock, Chmod(aklock, 0755))

if build['amd5']:
    md5 = aklock_env.Copy()
    md5.AppendUnique(CPPDEFINES = [ 'STAND_ALONE' ])
    md5.Program('amd5', md5.StaticObject(target = 'amd5.o', source = 'auth_md5.c'))

if build['asha1']:
    sha1 = aklock_env.Copy()
    sha1.AppendUnique(CPPDEFINES = [ 'STAND_ALONE' ])
    sha1.Program('asha1', sha1.StaticObject(target = 'asha1', source = 'auth_sha1.c'))

# vim:ft=python
