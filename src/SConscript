#!/bin/env python
##########################################################
#
# scons - buildsystem for alock
#
##########################################################

alock_sources = [ 'alock.c', 'cursors.c', 'auth_none.c' ]

Import('alock_env')

build =  alock_env.Copy()

if build['passwd']:
    alock_sources += [ 'auth_passwd.c' ]
if build['hash']:
    alock_sources += [ 'auth_md5.c', 'auth_sha1.c' ]
if build['pam']:
    alock_sources += [ 'auth_pam.c' ]


alock = build.Program('alock', alock_sources)
build.AddPostAction(alock, Chmod(alock, 0755))

if build['amd5']:
    md5 = alock_env.Copy()
    md5.AppendUnique(CPPDEFINES = [ 'STAND_ALONE' ])
    md5.Program('amd5', md5.StaticObject(target = 'amd5.o', source = 'auth_md5.c'))

if build['asha1']:
    sha1 = alock_env.Copy()
    sha1.AppendUnique(CPPDEFINES = [ 'STAND_ALONE' ])
    sha1.Program('asha1', sha1.StaticObject(target = 'asha1', source = 'auth_sha1.c'))

# vim:ft=python
